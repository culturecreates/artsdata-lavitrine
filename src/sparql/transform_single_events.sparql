PREFIX schema: <http://schema.org/>
PREFIX ex: <http://example.com/>

delete {
   ?s a  ex:EventForIndex , schema:Event .
}
insert {
  ?s a schema:EventSeries .
  ?s schema:subEvent ?s_subevent .
  ?s_subevent a schema:Event ; 
    schema:startDate ?startDate ; 
    schema:endDate ?endDate ;
    schema:name ?name ;
    schema:eventAttendanceMode ?eventAttendanceMode ;
    schema:eventStatus ?eventStatus ;
    schema:organizer ?organizer ;
    schema:offers ?offer .
    ?offer ?a ?b .
}
where {
  ?s a ex:EventForIndex , schema:Event .
  bind(URI(CONCAT(str(?s),"#SingleEvent"))  as  ?s_subevent)
  ?s schema:startDate ?startDate .
  ?s schema:location ?location .
  ?s schema:name ?name .
  OPTIONAL {
    ?s schema:endDate ?endDate .
  }
  OPTIONAL {
    ?s schema:organizer ?organizer .
  }
  OPTIONAL {
    ?s schema:eventAttendanceMode ?eventAttendanceMode .
  } 
  OPTIONAL {
    ?s schema:eventStatus ?eventStatus .
  } 
  OPTIONAL {
    ?s schema:offers ?offer .
    ?offer ?a ?b .
  }
}