PREFIX schema: <http://schema.org/>
PREFIX ex: <http://example.com/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adm: <http://kg.artsdata.ca/minted/>
construct {
    ?event ?p  ?o .
    ?event a ex:EventForIndex .
    ?o ?op ?oo .
    ?oo ?oop ?ooo .
    ?ooo ?ooop ?oooo .
    ?event schema:additionalType ?type .
    ?type skos:prefLabel ?prefLabel .
    ?type skos:closeMatch ?closeMatch .
    ?closeMatch skos:prefLabel ?closePrefLabel .   
}
where {
    {
        select * where {
            graph <http://kg.artsdata.ca/culture-creates/huginn/derived-grandtheatre-qc-ca> {
                {
                    select ?event 
                    where {
                        values ?type {
                             schema:Event schema:EventSeries
                        }  
                        ?event a ?type ;
                               schema:startDate ?startDate .
                        filter(not exists {
                                ?superEvent schema:subEvent ?event
                            })
                    } 
                    order by ?startDate
                    limit 10 offset 0 
                }
                ?event ?p  ?o .
                OPTIONAL {
                    ?o ?op ?oo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                    ?ooo ?ooop ?oooo .
                    filter(!isBlank(?oooo))
                }
                OPTIONAL {
                    ?event schema:additionalType ?type .
                }   
            } 
            
        }
    }
  
    OPTIONAL {
        ?event schema:additionalType ?type .
        graph <http://db.artsdata.ca/repositories/artsdata/rdf-graphs/grandtheatre-qc-ca-event-type-mapping> {
            ?type skos:prefLabel ?prefLabel .
            ?type skos:closeMatch ?closeMatch .
        }
        OPTIONAL {
            graph <http://kg.artsdata.ca/culture-creates/ontology/skos-event-types> {
                ?closeMatch skos:prefLabel ?closePrefLabel .
            }
        }
    }
}