PREFIX schema: <http://schema.org/>
PREFIX ex: <http://example.com/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adm: <http://kg.artsdata.ca/minted/>
PREFIX adr: <http://kg.artsdata.ca/resource/>

construct {
    ?event ?p  ?o .
    ?event a ex:EventForIndex .
    ?o ?op ?oo .
    ?oo ?oop ?ooo .
    ?ooo ?ooop ?oooo .
    ?event schema:additionalType ?type .
    ?type skos:prefLabel ?prefLabel .
    ?type a skos:Concept .
    ?type skos:closeMatch ?closeMatch .
    ?closeMatch skos:prefLabel ?closePrefLabel .   
    ?closeMatch a skos:Concept .
    ?event schema:location ?location .
    ?location ?loc_pred ?loc_obj ; schema:address ?address .
    ?address ?address_p ?addres_o .
        
}
where {
    {
        select * where {
            <graph_placeholder> schema:hasPart | schema:hasPart/schema:hasPart ?graph .
            graph ?graph {
                {
                    select ?event 
                    where {
                        values ?type {
                             schema:Event schema:EventSeries
                        }  
                        ?event a ?type ;
                               schema:startDate ?startDate .
                        OPTIONAL {
                            ?event  schema:endDate ?endDate .
                        }
                              
                        filter( not exists {
                                ?superEvent schema:subEvent ?event
                            })
                        filter( not exists { ?event schema:additionalType adr:Festival })
                        filter( not exists { ?event schema:additionalType <http://api.footlight.io/concepts/65719e94a5ac3d006314ed96> }) # Mauricie
                        filter( not exists { ?event schema:additionalType <http://api.footlight.io/concepts/6418ca425e4d4e0066a2ee9f> }) # Tout Culture
                        filter(?startDate >= now() || ?endDate >= now())
                    } 
                    order by ?startDate
                    limit 10 offset 0 
                }
                ?event ?p  ?o .
                OPTIONAL {
                    ?o ?op ?oo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                }
                OPTIONAL {
                    ?o ?op ?oo .
                    ?oo ?oop ?ooo .
                    ?ooo ?ooop ?oooo .
                    filter(!isBlank(?oooo))
                }
                OPTIONAL {
                    ?event schema:additionalType ?type .
                }   
            } 
            
        }
    }
    OPTIONAL {
        ?event schema:location ?location .
        graph ?location_graph {
            ?location ?loc_pred ?loc_obj ; schema:address ?address .
            ?address ?address_p ?addres_o .
        }
    }
    OPTIONAL {
        ?event schema:additionalType ?type .
        graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-gtq/grandtheatre-qc-ca-event-type-mapping> {
            ?type skos:prefLabel ?prefLabel .
            ?type skos:closeMatch ?closeMatch .
        }
        OPTIONAL {
            graph <http://kg.artsdata.ca/culture-creates/artsdata-data-model/skos-event-types> {
                ?closeMatch skos:prefLabel ?closePrefLabel .
            }
        }
    }
}